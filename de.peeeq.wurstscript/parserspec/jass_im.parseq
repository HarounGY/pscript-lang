package de.peeeq.wurstscript.jassIm


abstract syntax:
	
ImProg(ImVars globals, ImFunctions functions)

ImVars * ImVar
ImFunctions * ImFunction

ImVar(ImType type, String name)

ImType = 
	  ImSimpleType(String typename)
	| ImArrayType(String typename)
	| ImTupleType(ImTypes types)
	| ImVoid()
	
ImTypes * ImType

ImFunction(ImVars parameters, ImType returnType, ImVars locals, ImStmts body, boolean isNative)
	
ImStmts * ImStmt

ImStmt = 
	  ImIf(ImExpr condition, ImStmts thenBlock, ImStmts elseBlock)
	| ImLoop(ImStmts body)
	| ImExitwhen(ImExpr condition)
	| ImReturn(ImExprOpt returnValue)
	| ImSet(&ImVar left, ImExpr right)
	| ImSetTuple(&ImVar left, int tupleIndex, ImExpr right)
	| ImSetArray(&ImVar left, ImExpr index, ImExpr right)
	| ImSetArrayTuple(&ImVar left, ImExpr index, int tupleIndex, ImExpr right)
	| ImExpr
	
	
ImExprOpt = 
	  ImExpr
	| ImFlatExprOpt

ImFlatExprOpt = 
	  ImFlatExpr
	| ImNoExpr()
	
ImExprs * ImExpr
	
ImExpr = 
	  ImStatementExpr(ImStmts statements, ImExpr expr)
	| ImFlatExpr

ImFlatExpr = 
	  ImCall(&ImFunction func, ImExprs arguments)
	| ImVarAccess(&ImVar var)
	| ImVarArrayAccess(&ImVar var, ImExpr index)
	| ImTupleExpr(ImExprs exprs)
	| ImTupleSelection(ImExpr tupleExpr, int tupleIndex)
	| ImConst
	
	
ImConst = 
	  ImIntVal(int valI)
	| ImRealVal(String valR)
	| ImStringVal(String valS)
	| ImBoolVal(boolean valB)
	| ImFuncRef(&ImFunction func)
	| ImNull()
	
attributes:
	

	
ImStmt.flatten(
		java.util.List<ImStmt> stmts,
		de.peeeq.wurstscript.translation.imtranslation.Translator translator, 
		de.peeeq.wurstscript.jassIm.ImFunction f	
	)
	returns void
	implemented by de.peeeq.wurstscript.translation.imtranslation.Flatten.flatten
	
ImExpr.flattenExpr(
		java.util.List<ImStmt> stmts,
		de.peeeq.wurstscript.translation.imtranslation.Translator translator, 
		de.peeeq.wurstscript.jassIm.ImFunction f	
	)
	returns ImFlatExpr
	implemented by de.peeeq.wurstscript.translation.imtranslation.Flatten.flattenExpr
	
ImExprOpt.flattenExprOpt(
		java.util.List<ImStmt> stmts,
		de.peeeq.wurstscript.translation.imtranslation.Translator translator, 
		de.peeeq.wurstscript.jassIm.ImFunction f	
	)
	returns ImFlatExprOpt
	implemented by de.peeeq.wurstscript.translation.imtranslation.Flatten.flattenExpr
		
